plugins {
    id "org.gretty" version "4.1.10" apply false
    id 'test-report-aggregation'
    id 'org.sonarqube' version '7.2.2.6593'
    id "org.owasp.dependencycheck" version "12.1.9"
    id 'jacoco'
    id 'org.cyclonedx.bom' version '3.1.0'
}

ext {
    intygInfraVersion = System.properties['infraVersion'] ?: '4.0.0-SNAPSHOT'

    blockingSchemaVersion = "4.0.1.3"
    consentSchemaVersion = "2.0.1.2"
    internalMonitoringSchemaVersion = "1.0.4.3"
    intygClinicalprocessSchemasVersion = "1.0.24.2"
    intygHealthcondRehabilitationSchemasVersion = "1.1.3.2"
    rivtaItintegrationMonitoringSchemasVersion = "1.0.0.5.2"
    rivtaGetPersonsPersonSchemasVersion = "3.1.1.2"
    schemasContractVersion = "2.1.8.2"
    srsSchemasVersion = "0.0.16.2"

    commonsIoVersion = "2.14.0"
    cxfVersion = "4.0.5"
    googleGuavaVersion = "33.0.0-jre"
    jakartaJwsApiVersion = "3.0.0"
    jaxb2Version = "3.0.0"
    logbackEcsEncoderVersion = "1.6.0"
    picocliVersion = "4.7.5"
    poiVersion = "5.3.0"
    springSecurityExtensionsVersion = "1.0.3.RELEASE"
    shedlockSpringVersion = "5.16.0"

    springbootMavenBomVersion = "3.3.4"

    nvdMirror = 'https://vulnz-nvd-mirror-devtest.intyg.nordicmedtest.se'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'idea'

    group = 'se.inera.intyg.rehabstod'
    version = System.properties['buildVersion'] ?: '1.0.0-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    compileJava {
        options.encoding = "UTF-8"
        options.compilerArgs.add("-parameters")
    }

    dependencies {
        implementation platform("org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}")
        annotationProcessor platform("org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}")
        testAnnotationProcessor platform("org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}")

        implementation "ch.qos.logback:logback-classic"
        implementation "jakarta.jws:jakarta.jws-api:${jakartaJwsApiVersion}"

        testImplementation 'junit:junit'
        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testImplementation "org.junit.jupiter:junit-jupiter-params"
        testImplementation "org.mockito:mockito-junit-jupiter"
        testImplementation "org.mockito:mockito-core"

        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
        testRuntimeOnly "org.junit.vintage:junit-vintage-engine"

        compileOnly 'org.projectlombok:lombok'

        annotationProcessor 'org.projectlombok:lombok'
    }

    test { useJUnitPlatform() }

    repositories {
        mavenLocal()
        maven { url "https://nexus.drift.inera.se/repository/it-public/" }
        maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases/' }
        mavenCentral {
            content {
                excludeGroupByRegex "se\\.inera.*"
            }
        }
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.required = true
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        "**/testability/**",
                        "**/integrationtest/**"
                ])
            }))
        }
    }

    configurations {
        // Remove these due to conflict with Spring SAML which uses bcprov-jdk15.
        implementation.exclude group: "org.bouncycastle", module: "bcpkix-jdk15on"
        implementation.exclude group: "org.bouncycastle", module: "bcprov-jdk15on"
        implementation.exclude group: "org.bouncycastle", module: "bcprov-jdk15"
//        implementation.exclude group: "org.opensaml", module: "xmltooling"
    }
}

sonar {
    properties {
        property "sonar.projectKey", "intyg-rehabstod"
        property "sonar.projectName", "intyg-rehabstod"
        property "sonar.dependencyCheck.jsonReportPath", "${layout.buildDirectory.get()}/reports/dependency-check-report.json" as String
        property "sonar.dependencyCheck.htmlReportPath", "${layout.buildDirectory.get()}/reports/dependency-check-report.html" as String
    }
}

dependencyCheck {
    formats = ['HTML', 'JSON']
    nvd.setDatafeedUrl(nvdMirror)
    analyzers.setAssemblyEnabled(false)
    analyzers.setCentralEnabled(false)
    analyzers.ossIndex.setUsername(project.properties.get('ossIndexUsername') as String)
    analyzers.ossIndex.setPassword(project.properties.get('ossIndexPassword') as String)
}

reporting {
    subprojects.collect { subproject -> subproject.name }.each { subprojectName ->
        dependencies { testReportAggregation project(":${subprojectName}") }
    }
}
