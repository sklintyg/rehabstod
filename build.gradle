plugins {
    id "se.inera.intyg.plugin.common" version "3.2.7" apply false
    id "org.gretty" version "3.0.8" apply false
    id "nebula.node" version "1.4.2" apply false
    id "io.spring.dependency-management" version "1.0.13.RELEASE"
}

ext {
    intygInfraVersion = System.properties['infraVersion'] ?: '3.20.0-SNAPSHOT'
    refDataVersion = System.properties['refDataVersion'] ?: '1.0-SNAPSHOT'

    blockingSchemaVersion = "4.0.1.1"
    consentSchemaVersion = "2.0.1.1"
    internalMonitoringSchemaVersion = "1.0.4.1"
    intygClinicalprocessSchemasVersion = "1.0.24.1"
    intygHealthcondRehabilitationSchemasVersion = "1.1.3.1"
    rivtaItintegrationMonitoringSchemasVersion = "1.0.0.5.1"
    schemasContractVersion = "2.1.8.1"
    srsSchemasVersion = "0.0.16.1"

    commonsIoVersion = "2.11.0"
    cxfVersion = "3.5.5"
    googleGuavaVersion = "31.1-jre"
    itext7Version = "7.2.3"
    jadiraUserTypeVersion = "6.0.1.GA"
    javaxJwsApiVersion = "1.1"
    jaxb2Version = "0.13.1"
    jsonSimpleVersion = "1.1.1"
    picocliVersion = "4.6.3"
    poiVersion = "5.2.2"
    springSecurityExtensionsVersion = "1.0.3.RELEASE"
    shedlockSpringVersion = "4.42.0"
    testngVersion = "7.6.1"

    nodeJsVersion = "12.22.12"
    npmVersion = "6.14.16"

    springbootMavenBomVersion = "2.7.6"
}

allprojects {
    apply plugin: 'se.inera.intyg.plugin.common'
    apply plugin: 'maven'
    apply plugin: 'idea'
    apply plugin: "io.spring.dependency-management"

    repositories {
        maven { url "http://172.21.0.1:37373/repository/it-public/" }
        //maven { url "http://localhost:37373/repository/it-public/" }
        mavenLocal()
        maven { url "https://nexus.drift.inera.se/repository/it-public/" }
        mavenCentral {
            content {
                // this repository contains everything BUT artifacts with group starting with "se.inera"
                excludeGroupByRegex "se\\.inera.*"
            }
        }
    }

    test {
        useJUnitPlatform()
    }

    configurations {
        // Remove these due to conflict with Spring SAML which uses bcprov-jdk15.
        compile.exclude group: "org.bouncycastle", module: "bcpkix-jdk15on"
        compile.exclude group: "org.bouncycastle", module: "bcprov-jdk15on"
    }

    dependencyManagement {
        imports { mavenBom "org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}" }
    }

    dependencies {
        implementation "javax.jws:javax.jws-api:${javaxJwsApiVersion}"

        compile "ch.qos.logback:logback-classic"
        compile "org.slf4j:slf4j-api"

        testCompile "se.inera.intyg.refdata:refdata:${refDataVersion}"
        testCompile 'junit:junit'
        testCompile "org.junit.jupiter:junit-jupiter-api"
        testCompile "org.junit.jupiter:junit-jupiter-params"
        testCompile "org.mockito:mockito-junit-jupiter"
        testCompile "org.mockito:mockito-inline"

        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
        testRuntimeOnly "org.junit.vintage:junit-vintage-engine"
    }

    group = 'se.inera.intyg.rehabstod'
    version = System.properties['buildVersion'] ?: '0-SNAPSHOT'

    sourceCompatibility = 11
    targetCompatibility = 11

    task createVersionPropertyFile(type: VersionPropertyFileTask)

    uploadArchives.enabled = false

    uploadArchives.repositories.mavenDeployer {
        repository(url: "https://nexus.drift.inera.se/repository/maven-releases/") {
            authentication(userName: System.properties['ineraNexusUsername'], password: System.properties['ineraNexusPassword'])
        }
    }

    [install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
        pom.dependencies.findAll { dep -> dep.groupId == 'se.inera.intyg.infra' }.each {
            it.version = se.inera.intyg.IntygPluginKt.findResolvedVersion(project, 'se.inera.intyg.infra')
        }
    }
}

task tagRelease(type: TagReleaseTask)

def environment = project.findProperty('env') ?: "local"
se.inera.intyg.IntygPluginKt.addProjectPropertiesFromFile(project, file("$rootDir/env-properties/${environment}/settings.properties"))
