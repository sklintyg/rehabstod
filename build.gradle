plugins {
    alias(libs.plugins.org.sonarqube)
    alias(libs.plugins.org.owasp.dependencycheck)
    alias(libs.plugins.org.cyclonedx.bom)
    id "org.gretty" version "4.1.10" apply false
    id 'test-report-aggregation'
    id 'jacoco'
}

ext {
    intygBomVersion = '1.0.0.6'

    intygInfraVersion = System.properties['infraVersion'] ?: '4.1.0-SNAPSHOT'

    blockingSchemaVersion = "4.0.1.4"
    consentSchemaVersion = "2.0.1.3"
    intygClinicalprocessSchemasVersion = "1.1.0.1"
    intygHealthcondRehabilitationSchemasVersion = "1.2.0.1"
    schemasContractVersion = "2.2.0.1"
    srsSchemasVersion = "1.0.0.1"

    nvdMirror = 'https://vulnz-nvd-mirror-devtest.intyg.nordicmedtest.se'
}

apply from: 'gradle/configureIntygBom.gradle'

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'idea'

    group = 'se.inera.intyg.rehabstod'
    version = System.properties['buildVersion'] ?: '1.0.0-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    compileJava {
        options.encoding = "UTF-8"
        options.compilerArgs.add("-parameters")
    }

    dependencies {
        implementation platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        annotationProcessor platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        testImplementation platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        testAnnotationProcessor platform("se.inera.intyg.bom:platform:${intygBomVersion}")

        implementation "ch.qos.logback:logback-classic"

        testImplementation 'junit:junit'
        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testImplementation "org.junit.jupiter:junit-jupiter-params"
        testImplementation "org.mockito:mockito-junit-jupiter"
        testImplementation "org.mockito:mockito-core"

        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
        testRuntimeOnly "org.junit.vintage:junit-vintage-engine"

        compileOnly 'org.projectlombok:lombok'

        annotationProcessor 'org.projectlombok:lombok'
    }

    testing.suites {
        test { useJUnitJupiter() }
    }

    repositories {
        mavenLocal()
        maven { url "https://nexus.drift.inera.se/repository/it-public/" }
        maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases/' }
        mavenCentral {
            content {
                excludeGroupByRegex "se\\.inera.*"
            }
        }
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.required = true
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        "**/testability/**",
                        "**/integrationtest/**"
                ])
            }))
        }
    }

    configurations {
        // Remove these due to conflict with Spring SAML which uses bcprov-jdk15.
        implementation.exclude group: "org.bouncycastle", module: "bcpkix-jdk15on"
        implementation.exclude group: "org.bouncycastle", module: "bcprov-jdk15on"
        implementation.exclude group: "org.bouncycastle", module: "bcprov-jdk15"
//        implementation.exclude group: "org.opensaml", module: "xmltooling"
    }
}

sonar {
    properties {
        property "sonar.projectKey", "intyg-rehabstod"
        property "sonar.projectName", "intyg-rehabstod"
        property "sonar.dependencyCheck.jsonReportPath", "${layout.buildDirectory.get()}/reports/dependency-check-report.json" as String
        property "sonar.dependencyCheck.htmlReportPath", "${layout.buildDirectory.get()}/reports/dependency-check-report.html" as String
    }
}

dependencyCheck {
    formats = ['HTML', 'JSON']
    nvd.setDatafeedUrl(nvdMirror)
    analyzers.setAssemblyEnabled(false)
    analyzers.setCentralEnabled(false)
    analyzers.ossIndex.setUsername(project.properties.get('ossIndexUsername') as String)
    analyzers.ossIndex.setPassword(project.properties.get('ossIndexPassword') as String)
}

reporting {
    subprojects.collect { subproject -> subproject.name }.each { subprojectName ->
        dependencies { testReportAggregation project(":${subprojectName}") }
    }
}
