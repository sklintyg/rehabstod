plugins {
    id "se.inera.intyg.plugin.common" version "3.1.4" apply false
    id "org.gretty" version "3.0.1" apply false
    id "com.moowork.node" version "1.3.1" apply false
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
}

ext {
    intygInfraVersion = System.properties['infraVersion'] ?: '3.16.1-SNAPSHOT'
    refDataVersion = System.properties['refDataVersion'] ?: '1.0-SNAPSHOT'

    intygHealthcondRehabilitationSchemasVersion = "1.1.3"
    blockingSchemaVersion = "4.0.1"
    consentSchemaVersion = "2.0.1"
    rivtaItintegrationMonitoringSchemasVersion = "1.0.0.5"
    internalMonitoringSchemaVersion = "1.0.4"
    intygClinicalprocessSchemasVersion = "1.0.24"
    schemasContractVersion = "2.1.8"
    srsSchemasVersion = "0.0.16"

    apacheJstlVersion = "9.4.28.v20200408"
    commonsIoVersion = "2.6"
    cxfVersion = "3.2.4"
    googleGuavaVersion = "28.2-jre"
    hibernateVersion = "5.3.15.Final"
    itext7Version = "7.1.10"
    jadiraUserTypeVersion = "6.0.1.GA"
    jsonSimpleVersion = "1.1.1"
    poiVersion = "4.1.2"
    springSecurityExtensionsVersion = "1.0.3.RELEASE"
    shedlockSpringVersion = "4.5.0"

    powermockVersion = "2.0.6"
    testngVersion = "7.1.0"
    unitilsCoreVersion = "3.4.6"

    nodeJsVersion = "10.16.2"
    npmVersion = "6.10.3"

    springbootMavenBomVersion = "2.2.5.RELEASE"
}

allprojects {
    apply plugin: 'se.inera.intyg.plugin.common'
    apply plugin: 'maven'
    apply plugin: 'idea'
    apply plugin: "io.spring.dependency-management"

    repositories {
        mavenLocal()
        maven {
            url "https://nexus.drift.inera.se/repository/it-public/"
        }
        jcenter {
            content {
                // this repository contains everything BUT artifacts with group starting with "se.inera"
                excludeGroupByRegex "se\\.inera.*"
            }
        }
    }

    configurations {
        // Remove these due to conflict with Spring SAML which uses bcprov-jdk15.
        compile.exclude group: "org.bouncycastle", module: "bcpkix-jdk15on"
        compile.exclude group: "org.bouncycastle", module: "bcprov-jdk15on"
    }

    dependencyManagement {
        imports { mavenBom "org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}" }
    }

    dependencies {
        // Exclude and import of non-BOM version of Hibernate due to
        // bug in BOM-version 5.4.12.
        compile("org.hibernate:hibernate-core") {
            version {
                strictly "${hibernateVersion}"
            }
        }
        compile("org.hibernate:hibernate-hikaricp") {
            version {
                strictly "${hibernateVersion}"
            }
        }
        compile("org.hibernate:hibernate-entitymanager") {
            version {
                strictly "${hibernateVersion}"
            }
        }

        compile "ch.qos.logback:logback-classic"
        compile "org.slf4j:slf4j-api"

        testCompile "se.inera.intyg.refdata:refdata:${refDataVersion}"
        testCompile 'junit:junit:4.+'
        testCompile "org.mockito:mockito-core"
    }

    group = 'se.inera.intyg.rehabstod'
    version = System.properties['buildVersion'] ?: '0-SNAPSHOT'

    sourceCompatibility = 11
    targetCompatibility = 11

    task createVersionPropertyFile(type: VersionPropertyFileTask)

    uploadArchives.enabled = false

    uploadArchives.repositories.mavenDeployer {
        repository(url: "https://nexus.drift.inera.se/repository/maven-releases/") {
            authentication(userName: System.properties['ineraNexusUsername'], password: System.properties['ineraNexusPassword'])
        }
    }

    [install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
        pom.dependencies.findAll { dep -> dep.groupId == 'se.inera.intyg.infra' }.each {
            it.version = se.inera.intyg.IntygPluginKt.findResolvedVersion(project, 'se.inera.intyg.infra')
        }
    }
}

task tagRelease(type: TagReleaseTask)

def environment = project.findProperty('env') ?: "local"
se.inera.intyg.IntygPluginKt.addProjectPropertiesFromFile(project, file("$rootDir/env-properties/${environment}/settings.properties"))
