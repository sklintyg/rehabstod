import se.inera.intyg.IntygPluginKt

import java.nio.file.Files
import java.nio.file.Paths

plugins {
    id "se.inera.intyg.plugin.common" version "3.3.1" apply false
    id "io.spring.dependency-management" version "1.1.3" apply false
    id "com.netflix.nebula.node" version "3.0.0" apply false
    id "org.gretty" version "3.1.1" apply false
    id 'test-report-aggregation'
}

ext {
    intygInfraVersion = System.properties['infraVersion'] ?: '3.20.0-SNAPSHOT'
    refDataVersion = System.properties['refDataVersion'] ?: '1.0-SNAPSHOT'

    blockingSchemaVersion = "4.0.1.1"
    consentSchemaVersion = "2.0.1.1"
    internalMonitoringSchemaVersion = "1.0.4.1"
    intygClinicalprocessSchemasVersion = "1.0.24.1"
    intygHealthcondRehabilitationSchemasVersion = "1.1.3.1"
    rivtaItintegrationMonitoringSchemasVersion = "1.0.0.5.1"
    rivtaGetPersonsPersonSchemasVersion = "3.1.1.1"
    schemasContractVersion = "2.1.8.1"
    srsSchemasVersion = "0.0.16.1"

    commonsIoVersion = "2.14.0"
    cxfVersion = "3.6.2"
    googleGuavaVersion = "32.1.3-jre"
    itext7Version = "7.2.5"
    jakartaJwsApiVersion = "2.1.0" // version 3.0.0 -> warning: "class file for javax.jws.soap.SOAPBinding$ParameterStyle not found"
    jaxb2Version = "0.13.1"
    picocliVersion = "4.7.5"
    poiVersion = "5.2.4"
    springSecurityExtensionsVersion = "1.0.3.RELEASE"
    shedlockSpringVersion = "4.46.0"
    testngVersion = "7.8.0"

    nodeJsVersion = "12.22.12"
    npmVersion = "6.14.16"

    springbootMavenBomVersion = "2.7.17"
}

allprojects {
    apply plugin: 'se.inera.intyg.plugin.common'
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'idea'

    group = 'se.inera.intyg.rehabstod'
    version = System.properties['buildVersion'] ?: '0.0-SNAPSHOT'
    sourceCompatibility = 11
    targetCompatibility = 11

    dependencyManagement {
        imports { mavenBom "org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}" }
    }

    dependencies {
        implementation "ch.qos.logback:logback-classic"
        implementation "jakarta.jws:jakarta.jws-api:${jakartaJwsApiVersion}"

        testImplementation 'junit:junit'
        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testImplementation "org.junit.jupiter:junit-jupiter-params"
        testImplementation "org.mockito:mockito-junit-jupiter"
        testImplementation "org.mockito:mockito-inline"
        testImplementation "se.inera.intyg.refdata:refdata:${refDataVersion}"

        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
        testRuntimeOnly "org.junit.vintage:junit-vintage-engine"
    }

    tasks.register('createVersionPropertyFile', VersionPropertyFileTask)

    if (project.hasProperty('codeQuality')) {
        tasks.register('spotbugsIncludesAndExcludes') {
            dependsOn ":unzipBuildTools"
            def spotbugsToolDir = "${rootProject.layout.buildDirectory.get().asFile}/build-tools/spotbugs"
            spotbugsMain {
                includeFilter.set(file("${spotbugsToolDir}/spotbugsIncludeFilter.xml"))
                excludeFilter.set(file("${spotbugsToolDir}/spotbugsExcludeFilter.xml"))
            }
        }

        checkstyleMain.dependsOn createVersionPropertyFile
        jacocoTestReport.mustRunAfter createVersionPropertyFile
        spotbugsMain.dependsOn('spotbugsIncludesAndExcludes')
        spotbugsTest.enabled(false)
        dependencies { spotbugsSlf4j "org.slf4j:slf4j-simple" }
    }

    compileTestJava.dependsOn createVersionPropertyFile
    test { useJUnitPlatform() }

    repositories {
        mavenLocal()
        maven { url "https://nexus.drift.inera.se/repository/it-public/" }
        mavenCentral {
            content {
                excludeGroupByRegex "se\\.inera.*"
            }
        }
    }

    configurations {
        // Remove these due to conflict with Spring SAML which uses bcprov-jdk15.
        implementation.exclude group: "org.bouncycastle", module: "bcpkix-jdk15on"
        implementation.exclude group: "org.bouncycastle", module: "bcprov-jdk15on"
    }
}

subprojects.collect {subproject -> subproject.name }.each { subprojectName ->
    dependencies { testReportAggregation project(":${subprojectName}") }
}

tasks.register('tagRelease', TagReleaseTask)

tasks.register('unzipBuildTools', Copy) {
    into "${rootProject.layout.buildDirectory.get().asFile}/build-tools/"
    from { zipTree(project.rootProject.buildscript.configurations.classpath.find { it.name.contains("se.inera.intyg.plugin.common") })
            .matching { include "/spotbugs/**", "/git_hooks/**" }
    }
}

tasks.register('applyGitHooks', Copy) {
    def repository = IntygPluginKt.findGitRepository(project.rootProject.projectDir)
    def gitHooksDir = "${rootProject.layout.buildDirectory.get().asFile}/build-tools/git_hooks"
    def commitMsg = file("${gitHooksDir}/commit-msg")
    def preCommit = file("${gitHooksDir}/pre-commit")
    def toDir = Paths.get(repository.directory.path, "hooks")

    if (!Files.exists(toDir)) { Files.createDirectory(toDir) }
    IntygPluginKt.copyFile(commitMsg, toDir)
    IntygPluginKt.copyFile(preCommit, toDir)
}

applyGitHooks.dependsOn(unzipBuildTools)
