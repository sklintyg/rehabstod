apply plugin: 'war'
apply plugin: 'org.gretty'
apply plugin: 'nebula.grunt'

war.rootSpec.exclude "logback.xml"

test {
    exclude '**/*IT*'
    environment "catalina.base",  "${buildDir}/catalina.base"
}

task restAssuredTest(type: Test) {
    outputs.upToDateWhen { false }
    systemProperty "integration.tests.baseUrl", System.properties['baseUrl'] ?: "http://localhost:8030/"
    include '**/*IT*'
    environment "catalina.base",  "${buildDir}/catalina.base"
    useJUnitPlatform()
}

idea {
    module {
        excludeDirs += file('node_modules')
        excludeDirs += file('src/main/webapp/bower_components')
    }
}

node {
    version = "${nodeJsVersion}"
    npmVersion = "${npmVersion}"
    download = true
    distBaseUrl = 'https://nodejs.org/dist/'
    workDir = file("${rootProject.projectDir}/.gradle/nodejs")
    npmWorkDir = file("${rootProject.projectDir}/.gradle/npm")
    nodeModulesDir = file("${project.projectDir}")
}

if (project.hasProperty('useMinifiedJavaScript')) {
    war {
        webAppDirName = 'build/webapp'
    }
    war.dependsOn grunt_build
} else if (project.hasProperty('useNoMinifiedJavaScriptNoKarma')) {
    war.dependsOn grunt_build_no_minify_no_karma
} else {
    war.dependsOn grunt_build_no_minify
}

war.dependsOn createVersionPropertyFile

grunt_build.dependsOn npmInstall
grunt_build_no_minify.dependsOn npmInstall
grunt_build_no_minify_no_karma.dependsOn npmInstall

gretty {
    def minified = System.properties['useMinifiedJavaScript'] ?: false
    def instance = System.properties['instance'] ?: '0'
    def useHttpPort = 8030 + (instance as Integer)
    def useInternalHttpPort = 8130 + (instance as Integer)
    def applicationDir = "${rootProject.projectDir}/devops/dev"

    servletContainer = 'tomcat9'
    contextPath = '/'

    integrationTestTask = null
    scanInterval = 0
    debugPort = 8830 + (instance as Integer)
    portPropertiesFileName = "gretty_ports_${instance}.properties"
    debugSuspend = false
    fastReload = true
    inplaceMode = 'hard'
    enableNaming = true
    serverConfigFile = "tomcat-gretty.xml"

    jvmArgs = ["-Dcatalina.base=${buildDir}/catalina.base",
               "-Dspring.profiles.active=dev,rhs-samtyckestjanst-stub,rhs-sparrtjanst-stub,rhs-srs-stub,caching-enabled,rhs-security-test,rhs-wc-stub,ia-stub",
               "-Dlogback.file=${applicationDir}/config/logback-dev.xml",
               "-Djava.awt.headless=true",
               "-Dfile.encoding=UTF-8",
               "-Dapplication.dir=${applicationDir}",
               "-Ddev.config.file=${applicationDir}/config/application-dev.properties",
               "-DuseMinifiedJavaScript=${minified}",
               "-Ddev.http.port=${useHttpPort}",
               "-Ddev.http.port.internal=${useInternalHttpPort}"
    ]
}

dependencies {
    compile project(":rehabstod-it-integration")
    compile project(":rehabstod-samtyckestjanst-integration")
    compile project(":rehabstod-sparrtjanst-integration")
    compile project(":rehabstod-srs-integration")
    compile project(":rehabstod-wc-integration")
    compile project(":rehabstod-persistence")

    compile "se.riv.clinicalprocess.healthcond.monitoring:clinicalprocess-healthcond-monitoring-schemas:${internalMonitoringSchemaVersion}"

    compile "se.inera.intyg.schemas:schemas-contract:${schemasContractVersion}"

    compile "se.inera.intyg.infra:dynamiclink:${intygInfraVersion}"
    compile "se.inera.intyg.infra:hsatk-integration:${intygInfraVersion}"
    compile "se.inera.intyg.infra:monitoring:${intygInfraVersion}"
    compile "se.inera.intyg.infra:pu-integration:${intygInfraVersion}"
    compile "se.inera.intyg.infra:ia-integration:${intygInfraVersion}"
    compile "se.inera.intyg.infra:security-filter:${intygInfraVersion}"
    compile "se.inera.intyg.infra:security-siths:${intygInfraVersion}"
    compile "se.inera.intyg.infra:sjukfall-engine:${intygInfraVersion}"

    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml"
    compile ("com.googlecode.json-simple:json-simple:${jsonSimpleVersion}") {
        exclude group: "junit", module: "junit"
    }
    compile "com.itextpdf:itext7-core:${itext7Version}"

    compile "mysql:mysql-connector-java"
    compile "net.javacrumbs.shedlock:shedlock-spring:${shedlockSpringVersion}"
    compile "net.javacrumbs.shedlock:shedlock-provider-redis-spring:${shedlockSpringVersion}"

    compile "org.apache.activemq:activemq-spring"
    compile "org.apache.commons:commons-lang3"
    compile "org.apache.cxf:cxf-rt-features-logging:${cxfVersion}"
    compile "org.apache.cxf:cxf-rt-frontend-jaxws:${cxfVersion}"
    compile "org.apache.poi:poi-ooxml:${poiVersion}"
    compile "org.apache.poi:poi:${poiVersion}"

    compile "org.springframework.security.extensions:spring-security-saml2-core:${springSecurityExtensionsVersion}"
    compile "org.springframework.security:spring-security-config"
    compile "org.springframework.security:spring-security-web"
    compile "org.springframework.session:spring-session-data-redis"
    compile "org.springframework:spring-jms"
    compile "org.springframework.ws:spring-ws-core"

    compile "wsdl4j:wsdl4j"

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    compileOnly "jakarta.jms:jakarta.jms-api"
    compileOnly "javax.servlet:javax.servlet-api"
    compileOnly "jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api"

    testCompile "io.rest-assured:rest-assured"
    testCompile "io.rest-assured:json-schema-validator"

    testCompile "jakarta.jms:jakarta.jms-api"
    testCompile "org.hamcrest:hamcrest"
    testCompile "org.hamcrest:hamcrest-library"
    testCompile "org.springframework:spring-test"
    testCompile "org.testng:testng:${testngVersion}"

    gretty "jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api"
    gretty "org.eclipse.jetty:apache-jstl"
    gretty "se.inera.intyg.refdata:refdata:${refDataVersion}"
}
