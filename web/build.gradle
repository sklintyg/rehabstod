apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'
apply plugin: 'com.moowork.grunt'

war.rootSpec.exclude "logback.xml"

test {
    exclude '**/*IT*'
}

task restAssuredTest(type: Test) {
    outputs.upToDateWhen { false }
    systemProperty "integration.tests.baseUrl", System.properties['baseUrl'] ?: "http://localhost:8790/"
    include '**/*IT*'
}

idea {
    module {
        excludeDirs += file('node_modules')
        excludeDirs += file('src/main/webapp/bower_components')
    }
}

grunt {
    colors = true
}

node {
    version = '10.15.1'
    download = true
    distBaseUrl = 'https://build-inera.nordicmedtest.se/node/'
    workDir = file("${rootProject.projectDir}/nodejs")
}

if (project.hasProperty('rehabstod.useMinifiedJavaScript')) {
    war {
        webAppDirName = 'build/webapp'
    }
    war.dependsOn grunt_build
} else {
    war.dependsOn grunt_build_no_minify
}

war.dependsOn createVersionPropertyFile

task bowerInstall(type: NodeTask, dependsOn:npmInstall) {
    script = file('node_modules/bower/bin/bower')
    args = ["--config.storage.cache=${gradle.getGradleUserHomeDir()}/caches/bower/cache",
            "--config.storage.packages=${gradle.getGradleUserHomeDir()}/caches/bower/packages",
            "--config.storage.registry=${gradle.getGradleUserHomeDir()}/caches/bower/registry",
            'install']
    inputs.files file('bower.json')
    outputs.files file('src/main/webapp/bower_components')
}

grunt_build.dependsOn bowerInstall
grunt_build_no_minify.dependsOn bowerInstall

task zipResources(type: ArchiveDirectoryTask) {
    sourcePath "../src/main/resources"
    include "*"
    include "*/*" //to include contents of a folder present inside from directory
    archiveName "resources.zip"
    destinationDir(file("../devops/openshift/test/env"))
    archiveOnlyIfDiff file("${destinationDir}/${archiveName}").exists()
}

build.dependsOn zipResources

gretty {
    contextPath = '/'
    contextConfigFile = "${projectDir}/src/main/resources/jetty-web.xml"
    // logbackConfigFile = "${projectDir}/src/main/resources/logback.xml"

    integrationTestTask = null
    scanInterval = 0
    httpPort = 8790
    debugPort = 5008
    debugSuspend = false
    fastReload = true
    inplaceMode = 'hard'
    enableNaming = true

    jvmArgs = [
            '-Dspring.profiles.active=dev,rhs-samtyckestjanst-stub,rhs-sparrtjanst-stub,rhs-it-stub,rhs-srs-stub,caching-enabled',
               '-Dconfig.file=' + projectDir + '/src/main/resources/dev.properties',
               '-Dcredentials.file=' + projectDir + '/src/main/resources/dev-credentials.properties',
               '-Dresources.folder=' +projectDir + '/../src/main/resources'
               ]

}

dependencies {
    compile project(":rehabstod-it-integration")
    compile project(":rehabstod-samtyckestjanst-integration")
    compile project(":rehabstod-sparrtjanst-integration")
    compile project(":rehabstod-srs-integration")
    compile project(":rehabstod-persistence")

    compile "se.riv.clinicalprocess.healthcond.monitoring:clinicalprocess-healthcond-monitoring-schemas:${internalMonitoringSchemaVersion}"

    compile "se.inera.intyg.schemas:schemas-contract:${schemasContractVersion}"

    compile "se.inera.intyg.infra:dynamiclink:${intygInfraVersion}"
    compile "se.inera.intyg.infra:hsa-integration:${intygInfraVersion}"
    compile "se.inera.intyg.infra:monitoring:${intygInfraVersion}"
    compile "se.inera.intyg.infra:pu-integration:${intygInfraVersion}"
    compile "se.inera.intyg.infra:security-filter:${intygInfraVersion}"
    compile "se.inera.intyg.infra:security-siths:${intygInfraVersion}"
    compile "se.inera.intyg.infra:sjukfall-engine:${intygInfraVersion}"

    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"
    compile "com.googlecode.json-simple:json-simple:${jsonSimpleVersion}"
    compile "com.itextpdf:itextpdf:${itextVersion}"
    compile "org.apache.commons:commons-lang3:${commonsLang3Version}"
    compile "org.apache.poi:poi-ooxml:${poiVersion}"
    compile "org.apache.poi:poi:${poiVersion}"
    compile "org.springframework.security.extensions:spring-security-saml2-core:${springSecurityExtensionsVersion}"
    compile "org.springframework.security:spring-security-config:${springSecurityVersion}"
    compile "org.springframework.security:spring-security-web:${springSecurityVersion}"
    compile "org.springframework.ws:spring-ws-core:${springWsCoreVersion}"
    compile "org.springframework:spring-jms:${springContextVersion}"
    compile "org.springframework.session:spring-session:${springSessionVersion}"
    compile "wsdl4j:wsdl4j:${wsdl4jVersion}"

    compile "org.apache.activemq:activemq-spring:${activemqVersion}"
    compile "mysql:mysql-connector-java:${mysqlVersion}"

    compileOnly "javax.jms:jms-api:${javaxJmsVersion}"
    compileOnly "javax.servlet:javax.servlet-api:${javaxServletVersion}"
    compileOnly "jstl:jstl:${jstlVersion}"

    testCompile "com.jayway.restassured:rest-assured:${restAssuredVersion}"
    testCompile "com.jayway.restassured:json-schema-validator:${restAssuredVersion}"
    testCompile "org.hamcrest:hamcrest-all:${hamcrestVersion}"
    testCompile "org.powermock:powermock-mockito-release-full:${powermockFullVersion}"
    testCompile "org.springframework:spring-test:${springContextVersion}"
    testCompile "org.unitils:unitils-core:${unitilsCoreVersion}"
    testCompile "javax.jms:jms-api:${javaxJmsVersion}"
}
