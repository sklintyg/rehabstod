apply plugin: 'war'
apply plugin: 'org.gretty'
apply plugin: 'com.moowork.grunt'

war.rootSpec.exclude "logback.xml"

test {
    exclude '**/*IT*'
    environment "catalina.base",  "${buildDir}/catalina.base"
}

task restAssuredTest(type: Test) {
    outputs.upToDateWhen { false }
    systemProperty "integration.tests.baseUrl", System.properties['baseUrl'] ?: "http://localhost:8790/"
    include '**/*IT*'
    environment "catalina.base",  "${buildDir}/catalina.base"
}

idea {
    module {
        excludeDirs += file('node_modules')
        excludeDirs += file('src/main/webapp/bower_components')
    }
}

node {
    version = "${nodeJsVersion}"
    npmVersion = "${npmVersion}"
    download = true
    distBaseUrl = 'https://build-inera.nordicmedtest.se/node/'
    workDir = file("${rootProject.projectDir}/.gradle/nodejs")
    npmWorkDir = file("${rootProject.projectDir}/.gradle/npm")
    nodeModulesDir = file("${project.projectDir}")
}

if (project.hasProperty('useMinifiedJavaScript')) {
    war {
        webAppDirName = 'build/webapp'
    }
    war.dependsOn grunt_build
} else {
    war.dependsOn grunt_build_no_minify
}

war.dependsOn createVersionPropertyFile

grunt_build.dependsOn npmInstall
grunt_build_no_minify.dependsOn npmInstall

gretty {
    servletContainer = 'tomcat9'
    contextPath = '/'

    integrationTestTask = null
    scanInterval = 0
    httpPort = 8790
    debugPort = 5008
    debugSuspend = false
    fastReload = true
    inplaceMode = 'hard'
    enableNaming = true

    jvmArgs = [
            '-Dspring.profiles.active=dev,rhs-samtyckestjanst-stub,rhs-sparrtjanst-stub,rhs-it-stub,rhs-srs-stub,caching-enabled,rhs-security-test,rhs-wc-stub,ia-stub',
            //'-Dspring.profiles.active=dev,rhs-samtyckestjanst-stub,rhs-sparrtjanst-stub,rhs-srs-stub,caching-enabled,rhs-security-test,ia-stub',
               '-Dconfig.folder=' + projectDir + '/../devops/dev',
               '-Dconfig.file=' + projectDir + '/src/main/resources/dev.properties',
               '-Dcredentials.file=' + projectDir + '/src/main/resources/dev-credentials.properties',
               '-Dresources.folder=classpath:',
               '-Dcatalina.base=' + buildDir + '/catalina.base'
               ]

}

dependencies {
    compile project(":rehabstod-it-integration")
    compile project(":rehabstod-samtyckestjanst-integration")
    compile project(":rehabstod-sparrtjanst-integration")
    compile project(":rehabstod-srs-integration")
    compile project(":rehabstod-wc-integration")
    compile project(":rehabstod-persistence")

    compile "se.riv.clinicalprocess.healthcond.monitoring:clinicalprocess-healthcond-monitoring-schemas:${internalMonitoringSchemaVersion}"

    compile "se.inera.intyg.schemas:schemas-contract:${schemasContractVersion}"

    compile "se.inera.intyg.infra:dynamiclink:${intygInfraVersion}"
    compile "se.inera.intyg.infra:hsa-integration:${intygInfraVersion}"
    compile "se.inera.intyg.infra:monitoring:${intygInfraVersion}"
    compile "se.inera.intyg.infra:pu-integration:${intygInfraVersion}"
    compile "se.inera.intyg.infra:ia-integration:${intygInfraVersion}"
    compile "se.inera.intyg.infra:security-filter:${intygInfraVersion}"
    compile "se.inera.intyg.infra:security-siths:${intygInfraVersion}"
    compile "se.inera.intyg.infra:sjukfall-engine:${intygInfraVersion}"

    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml"
    compile ("com.googlecode.json-simple:json-simple:${jsonSimpleVersion}") {
        exclude group: "junit", module: "junit"
    }
    compile "com.itextpdf:itext7-core:${itext7Version}"

    compile "mysql:mysql-connector-java"
    compile "net.javacrumbs.shedlock:shedlock-spring:${shedlockSpringVersion}"
    compile "net.javacrumbs.shedlock:shedlock-provider-redis-spring:${shedlockSpringVersion}"

    compile "org.apache.activemq:activemq-spring"
    compile "org.apache.commons:commons-lang3"
    compile "org.apache.cxf:cxf-rt-features-logging:${cxfVersion}"
    compile "org.apache.poi:poi-ooxml:${poiVersion}"
    compile "org.apache.poi:poi:${poiVersion}"

    compile "org.springframework.security.extensions:spring-security-saml2-core:${springSecurityExtensionsVersion}"
    compile "org.springframework.security:spring-security-config"
    compile "org.springframework.security:spring-security-web"
    compile "org.springframework.session:spring-session-data-redis"
    compile "org.springframework:spring-jms"
    compile "org.springframework.ws:spring-ws-core"

    compile "wsdl4j:wsdl4j"

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    compileOnly "jakarta.jms:jakarta.jms-api"
    compileOnly "javax.servlet:javax.servlet-api"
    compileOnly "jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api"

    testCompile "io.rest-assured:rest-assured"
    testCompile "io.rest-assured:json-schema-validator"
    testCompile "com.sun.xml.bind:jaxb-osgi:2.3.2" // Can remove when rest-assured in BOM > version 4.0

    testCompile "jakarta.jms:jakarta.jms-api"
    testCompile "org.hamcrest:hamcrest"
    testCompile "org.hamcrest:hamcrest-library"
    testCompile "org.powermock:powermock-module-junit4:${powermockVersion}"
    testCompile "org.powermock:powermock-api-mockito2:${powermockVersion}"
    testCompile "org.springframework:spring-test"
    testCompile "org.unitils:unitils-core:${unitilsCoreVersion}"
    testCompile "org.testng:testng:${testngVersion}"

    gretty "jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api"
    gretty "org.eclipse.jetty:apache-jstl:${apacheJstlVersion}"
    gretty "se.inera.intyg.refdata:refdata:${refDataVersion}"
}
